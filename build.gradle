plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def allureVersion = "2.20.0",
        selenideVersion = "7.7.3",
        junitVersion = "5.10.0",
        slf4Version = "2.0.12",
        ownerVersion = "1.0.12",
        fakerVersion = "1.0.2"


allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4Version",
            "org.aeonbits.owner:owner:$ownerVersion",
            "com.github.javafaker:javafaker:$fakerVersion"

    )
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task web_test(type: Test) {
    useJUnitPlatform {
        includeTags("Web")
    }
}

task negative_test(type: Test) {
    useJUnitPlatform {
        includeTags("NEGATIVE")
    }
}

task regress_test {
    dependsOn web_test, negative_test
}
